000000*    Zone d'indentification
000001 IDENTIFICATION DIVISION.
000002 PROGRAM-ID. LIVRET01.
000003*    Configuration de l'environnement
000004 ENVIRONMENT DIVISION.
000005 CONFIGURATION SECTION.
000006 SPECIAL-NAMES. DECIMAL-POINT IS COMMA.
000007*    Configuration des inputs
000008 INPUT-OUTPUT SECTION.
000009 FILE-CONTROL.
000010*    ---Fichier de configuration
000011     SELECT FICHIER-CONFIGURATION ASSIGN TO "FICHIER.CONFIG"
000012     ORGANIZATION IS SEQUENTIAL
000013     FILE STATUS IS WS-FILE-CONFIG-STATUS.
000014     
000015*    ---Fichier de transactions
000016     SELECT FICHIER-TRANSACTIONS ASSIGN TO "FICHIER.LIVRET"
000017     ORGANIZATION IS SEQUENTIAL
000018     FILE STATUS IS WS-FILE-TRANS-STATUS.
000019     
000020*    Configuration des données
000021*    ---Configuration des données externes
000022 DATA DIVISION.
000023 FILE SECTION.
000024*    -------Configuration des données de configuration
000025 FD FICHIER-CONFIGURATION.
000026 01 CONFIGURATION-ENREG.
000027    05 TAUX-INTERETS-CONF  PIC 9(1)V99.
000028    05 FILLER              PIC X(169).
000029*    -------Configuration des données de transactions
000030 FD FICHIER-TRANSACTIONS.
000031 01 TRANSACTION-ENREG.
000032    05 TYPE-TRANSACTION    PIC X.
000033    05 MONTANT-TRANSACTION PIC 9(6)V99.
000034    05 FILLER              PIC X(2).
000035*    ---Configuration des données internes
000036 WORKING-STORAGE SECTION.
000037 01 WS-FILE-TRANS-STATUS      PIC X(02).
000038 01 WS-FILE-CONFIG-STATUS     PIC X(02).
000039 01 WS-DATA.
000040    05 WS-TYPE-TRANSACTION    PIC X.
000041    05 WS-MONTANT-TRANSACTION PIC 9(6)V99.
000042 01 WS-SOLDE                  PIC 9(8)V99 VALUE 0.
000043 01 WS-TOTAL-DEPOTS           PIC 9(8)V99 VALUE 0.
000044 01 WS-TOTAL-RETRAITS         PIC 9(8)V99 VALUE 0.
000045 01 WS-INTERETS-ANNUELS       PIC 9(8)V99 VALUE 0.
000046 01 WS-EOF                    PIC X.
000047    88 EOF                    VALUE "Y".
000048    88 NOT-EOF                VALUE "N".
000049 01 WS-TAUX-INTERETS          PIC 9V99 VALUE 0,03.
000050*    ---Configuration des données finales à afficher
000051 01 WS-MONTANTS-EDITES.
000052    05 WS-SOLDE-ED            PIC Z(9)9,99.
000053    05 WS-INTERETS-ANNUELS-ED PIC Z(9)9,99.
000054    05 WS-TOTAL-DEPOTS-ED     PIC Z(9)9,99.
000055    05 WS-TOTAL-RETRAITS-ED   PIC Z(9)9,99.
000056*    ---Configuration des données de débug
000057d 01 DEBUG-DATA.
000058d   05 D-TYPE-TRANSACTION     PIC X.
000059d   05 D-MONTANT-TRANSACTION  PIC 9(6)V99.
000060*    Configuration du programme
000061 PROCEDURE DIVISION.
000062*    ---Tentative d'ouverture du fichier de données
000063     OPEN INPUT FICHIER-CONFIGURATION
000064     IF WS-FILE-CONFIG-STATUS NOT = "00"
000065        DISPLAY "ERREUR OUVERTURE FICHIER, STATUT : "
000066                 WS-FILE-CONFIG-STATUS
000067     END-IF.
000068     PERFORM TRT-CONFIGURATION
000069     CLOSE FICHIER-CONFIGURATION
000070*    ---Tentative d'ouverture du fichier de données
000071     OPEN INPUT FICHIER-TRANSACTIONS
000072     IF WS-FILE-TRANS-STATUS NOT = "00"
000073        DISPLAY "ERREUR OUVERTURE FICHIER, STATUT : "
000074                 WS-FILE-TRANS-STATUS
000075     END-IF.
000076*    ---Traitement principal : Récupération des données
000077     PERFORM TRT-PRINCIPAL
000078     CLOSE FICHIER-TRANSACTIONS
000079*    ---Traitement de l'affichage
000080     PERFORM AFFICHER-RESULTATS
000081*    ---Arrêt du programme
000082     STOP RUN.
000083*    Définition du traitement des données de configuration
000084 TRT-CONFIGURATION.
000085     SET NOT-EOF TO TRUE
000086     PERFORM UNTIL EOF
000087         READ FICHIER-CONFIGURATION
000088             AT END
000089                 SET EOF TO TRUE
000090             NOT AT END
000091                 INITIALIZE WS-DATA
000092                 MOVE TAUX-INTERETS-CONF TO WS-TAUX-INTERETS
000093         END-READ
000094     END-PERFORM
000095     .
000096*    Définition du traitement principal
000097 TRT-PRINCIPAL.
000098     SET NOT-EOF TO TRUE
000099*    ---Récupération des données jusqu'à la fin du fichier
000100     PERFORM UNTIL EOF
000101         READ FICHIER-TRANSACTIONS
000102*    --------Vérification de la position dans le fichier
000103             AT END
000104             SET EOF TO TRUE
000105             NOT AT END
000106*    --------Initialisation des données du fichiers
000107*             INITIALIZE WS-DATA
000108             MOVE TYPE-TRANSACTION TO WS-TYPE-TRANSACTION
000109             MOVE MONTANT-TRANSACTION TO WS-MONTANT-TRANSACTION
000110d            MOVE TYPE-TRANSACTION TO D-TYPE-TRANSACTION
000111d            MOVE MONTANT-TRANSACTION TO D-MONTANT-TRANSACTION
000112d            DISPLAY D-TYPE-TRANSACTION
000113d            DISPLAY D-MONTANT-TRANSACTION
000114*    --------Vérification du type de la transaction
000115             IF WS-TYPE-TRANSACTION = "D"
000116                ADD WS-MONTANT-TRANSACTION TO WS-SOLDE
000117                ADD WS-MONTANT-TRANSACTION TO WS-TOTAL-DEPOTS
000118             ELSE IF WS-TYPE-TRANSACTION = "R"
000119                SUBTRACT WS-MONTANT-TRANSACTION FROM WS-SOLDE
000120                ADD WS-MONTANT-TRANSACTION TO WS-TOTAL-RETRAITS
000121                  END-IF
000122             END-IF
000123         END-READ
000124     END-PERFORM
000125     .
000126*    Définition de l'affichage des résultats
000127 AFFICHER-RESULTATS.
000128*    ---Calcul des différents résultats
000129     COMPUTE WS-INTERETS-ANNUELS = WS-SOLDE * WS-TAUX-INTERETS
000130     ADD WS-INTERETS-ANNUELS TO WS-SOLDE.
000131     MOVE WS-SOLDE TO WS-SOLDE-ED
000132     MOVE WS-INTERETS-ANNUELS TO WS-INTERETS-ANNUELS-ED
000133     MOVE WS-TOTAL-DEPOTS TO WS-TOTAL-DEPOTS-ED
000134     MOVE WS-TOTAL-RETRAITS TO WS-TOTAL-RETRAITS-ED
000135*    ---Affichage des résulats
000136     DISPLAY 'Solde final : ' WS-SOLDE-ED
000137     DISPLAY 'Intérêts : ' WS-INTERETS-ANNUELS-ED
000138     DISPLAY 'Total dépôts : ' WS-TOTAL-DEPOTS-ED
000139     DISPLAY 'Total retraits : ' WS-TOTAL-RETRAITS-ED
000140     .
           
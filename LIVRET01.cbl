000000*    Zone d'indentification
000001 IDENTIFICATION DIVISION.
000002 PROGRAM-ID. LIVRET01.
000003*    Configuration de l'environnement
000004 ENVIRONMENT DIVISION.
000005 CONFIGURATION SECTION.
000006 SPECIAL-NAMES. DECIMAL-POINT IS COMMA.
000007*    Configuration des inputs
000008 INPUT-OUTPUT SECTION.
000009 FILE-CONTROL.
000010     SELECT FICHIER-TRANSACTIONS ASSIGN TO "FICHIER.LIVRET"
000011     ORGANIZATION IS SEQUENTIAL
000012     FILE STATUS IS WS-FILE-STATUS.
000013*    Configuration des données
000014*    ---Configuration des données externes
000015 DATA DIVISION.
000016 FILE SECTION.
000017 FD FICHIER-TRANSACTIONS.
000018 01 TRANSACTION-ENREG.
000019    05 TYPE-TRANSACTION    PIC X.
000020    05 MONTANT-TRANSACTION PIC 9(6)V99.
000021    05 FILLER              PIC X(2).
000022*    ---Configuration des données internes
000023 WORKING-STORAGE SECTION.
000024 01 WS-FILE-STATUS            PIC X(02).
000025 01 WS-DATA.
000026    05 WS-TYPE-TRANSACTION    PIC X.
000027    05 WS-MONTANT-TRANSACTION PIC 9(6)V99.
000028 01 WS-SOLDE                  PIC 9(8)V99 VALUE 0.
000029 01 WS-TOTAL-DEPOTS           PIC 9(8)V99 VALUE 0.
000030 01 WS-TOTAL-RETRAITS         PIC 9(8)V99 VALUE 0.
000031 01 WS-INTERETS-ANNUELS       PIC 9(8)V99 VALUE 0.
000032 01 WS-EOF                    PIC X.
000033    88 EOF                    VALUE "Y".
000034    88 NOT-EOF                VALUE "N".
000035 01 WS-TAUX-INTERETS          PIC 9V99 VALUE 0,03.
000036*    ---Configuration des données finales à afficher
000037 01 WS-MONTANTS-EDITES.
000038    05 WS-SOLDE-ED            PIC Z(9)9,99.
000039    05 WS-INTERETS-ANNUELS-ED PIC Z(9)9,99.
000040    05 WS-TOTAL-DEPOTS-ED     PIC Z(9)9,99.
000041    05 WS-TOTAL-RETRAITS-ED   PIC Z(9)9,99.
000042*    ---Configuration des données de débug
000043d 01 DEBUG-DATA.
000044d   05 D-TYPE-TRANSACTION     PIC X.
000045d   05 D-MONTANT-TRANSACTION  PIC 9(6)V99.
000046*    Configuration du programme
000047 PROCEDURE DIVISION.
000048*    ---Tentative d'ouverture du fichier de données
000049     OPEN INPUT FICHIER-TRANSACTIONS
000050     IF WS-FILE-STATUS NOT = "00"
000051        DISPLAY "ERREUR OUVERTURE FICHIER, STATUT : "
000052                 WS-FILE-STATUS
000053     END-IF.
000054*    ---Traitement principal : Récupération des données
000055     PERFORM TRT-PRINCIPAL
000056     CLOSE FICHIER-TRANSACTIONS
000057*    ---Traitement de l'affichage
000058     PERFORM AFFICHER-RESULTATS
000059*    ---Arrêt du programme
000060     STOP RUN.
000061*    Définition du traitement principal
000062 TRT-PRINCIPAL.
000063     SET NOT-EOF TO TRUE
000064*    ---Récupération des données jusqu'à la fin du fichier
000065     PERFORM UNTIL EOF
000066         READ FICHIER-TRANSACTIONS
000067*    --------Vérification de la position dans le fichier
000068             AT END
000069             SET EOF TO TRUE
000070             NOT AT END
000071*    --------Initialisation des données du fichiers
000072             INITIALIZE WS-DATA
000073             MOVE TYPE-TRANSACTION TO WS-TYPE-TRANSACTION
000074             MOVE MONTANT-TRANSACTION TO WS-MONTANT-TRANSACTION
000075d            MOVE TYPE-TRANSACTION TO D-TYPE-TRANSACTION
000076d            MOVE MONTANT-TRANSACTION TO D-MONTANT-TRANSACTION
000077d            DISPLAY D-TYPE-TRANSACTION
000078d            DISPLAY D-MONTANT-TRANSACTION
000079*    --------Vérification du type de la transaction
000080             IF WS-TYPE-TRANSACTION = "D"
000081                ADD WS-MONTANT-TRANSACTION TO WS-SOLDE
000082                ADD WS-MONTANT-TRANSACTION TO WS-TOTAL-DEPOTS
000083             ELSE IF WS-TYPE-TRANSACTION = "R"
000084                SUBTRACT WS-MONTANT-TRANSACTION FROM WS-SOLDE
000085                ADD WS-MONTANT-TRANSACTION TO WS-TOTAL-RETRAITS
000086                  END-IF
000087             END-IF
000088         END-READ
000089     END-PERFORM
000090     .
000091*    Définition de l'affichage des résultats
000092 AFFICHER-RESULTATS.
000093*    ---Calcul des différents résultats
000094     COMPUTE WS-INTERETS-ANNUELS = WS-SOLDE * WS-TAUX-INTERETS
000095     ADD WS-INTERETS-ANNUELS TO WS-SOLDE.
000096     MOVE WS-SOLDE TO WS-SOLDE-ED
000097     MOVE WS-INTERETS-ANNUELS TO WS-INTERETS-ANNUELS-ED
000098     MOVE WS-TOTAL-DEPOTS TO WS-TOTAL-DEPOTS-ED
000099     MOVE WS-TOTAL-RETRAITS TO WS-TOTAL-RETRAITS-ED
000100*    ---Affichage des résulats
000101     DISPLAY 'Solde final : ' WS-SOLDE-ED
000102     DISPLAY 'Intérêts : ' WS-INTERETS-ANNUELS-ED
000103     DISPLAY 'Total dépôts : ' WS-TOTAL-DEPOTS-ED
000104     DISPLAY 'Total retraits : ' WS-TOTAL-RETRAITS-ED
000105     .
           